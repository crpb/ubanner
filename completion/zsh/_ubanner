#compdef ubanner

# Copyright Â© 2024 Christopher Bock <christopher@bocki.com>
# SPDX-License-Identifier: MIT

# SHA-256(help) = ddbff3e9b1bb9b9d8f33022c1d7f768f8325d23f595fc48c34438bd4b8301d0e

local arguments

arguments=(
  '--trim[trim leading/trailing empty lines]'
  '(-f --font)'{-f,--font}'[use this font face]:font:->fonts'
  + '(fs)'
  '(-s --font-size)'{-s,--font-size}'[font size in pixels]: :_numbers -d32 -l16 -m320'
  '(-S --full-screen)'{-S,--full-screen}'[max font size to fit terminal window]'
  + '(excl)'
  {-h,--help}'[show help]'
  '--version[show version information and exit]'
  '--list-fonts[print list of available font faces]'
)
_arguments -S $arguments
case "$state" in
  fonts)
    local -a fontfaces
    fontfaces=("${(@f)$(ubanner --list-fonts)}")
    _values "fontname" $fontfaces
    ;;
esac
