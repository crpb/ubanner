#compdef ubanner

# Copyright Â© 2024 Christopher Bock <christopher@bocki.com>
# SPDX-License-Identifier: MIT

# SHA-256(help) = f0738bb0092f0a0d77fd30ce308b8ffd32f4e687bca8d5e1ca58a2e044454712

local arguments

arguments=(
  '--trim[trim leading/trailing empty lines]'
  '(-f --font)'{-f,--font}'[use this font face]:font:->fonts'
  + '(fs)'
  '(-s --font-size)'{-s,--font-size}'[font size in pixels]: :_numbers -d32 -l16 -m320'
  '(-S --full-screen)'{-S,--full-screen}'[max font size to fit terminal window]'
  + '(excl)'
  {-h,--help}'[show help]'
  '--version[show version information and exit]'
  '--list-fonts[print list of available font faces]'
)
_arguments -S $arguments
case "$state" in
  fonts)
    local -a fontfaces
    fontfaces=("${(@f)$(ubanner --list-fonts)}")
    _values "fontname" $fontfaces
    ;;
esac
